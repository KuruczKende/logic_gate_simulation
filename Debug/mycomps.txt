_xor:a^b
_not:~a
_or:a|b
_and:a&b
_andnot:a&~b
_up:[a]a^b
_hadder:a^b,a&b
_dff:[b]a
_sr_in:(c|a)&~b
_sr:_sr_in(abc,c)
_d_in:[b]b&a|(~b&c)
_d:_d_in(abc,c)
_d_r_in:[bc](b&a|(~b&d))&~c
_d_r:_d_r_in(abcd,d)
_d_hadder:_hadder(ad,Ae)_d_r(Abc,d)
_xor_c:_xor(ab,b)
_mux2:(a&~c)|(b&c)
_mux4b2:_mux2(aei,j)_mux2(bfi,k)_mux2(cgi,l)_mux2(dhi,m)
_mux4:(a&~e&~f)|(b&~e&f)|(c&e&~f)|(d&e&f)
_mux4b4:_mux4(aeimqr,s)_mux4(bfjnqr,t)_mux4(cgkoqr,u)_mux4(dhlpqr,v)
_and4:a&e,b&f,c&g,d&h
_or4:a|e,b|f,c|g,d|h
_xor4:a^e,b^f,c^g,d^h
_not4:~a,~b,~c,~d
_alu_log:_and4(abcdefgh,ABCD)_or4(abcdefgh,EFGH)_xor4(abcdefgh,IJKL)_not4(abcd,MNOP)_mux4b4(ABCDEFGHIJKLMNOPij,klmn)
_adder:_hadder(ab,AB)_hadder(Ac,dC)_or(BC,e)
_adder4:_adder(dhi,mA)_adder(cgA,lB)_adder(bfB,kC)_adder(aeC,jn)
_suber:a^b^c,b&c|~a&(b^c)
_suber4:_suber(dhi,mA)_suber(cgA,lB)_suber(bfB,kC)_suber(aeC,jn)
_alu_alg:_adder4(abcdefghj,ABCDE)_adder4(abcdefghk,FGHIJ)_suber4(abcdefgh0,KLMNO)_mux4b4(ABCDABCDFGHIKLMNij,lmno)_mux4(EEJOij,p)
_alu:_alu_log(abcdefghij,ABCD)_alu_alg(abcdefghijl,EFGHI)_mux4b2(ABCDEFGHk,mnop)_mux2(0Ik,q)
_is40:~a&~b
_is41:~a&b
_is42:a&~b
_is43:a&b
_mem4b:_d_r(aef,g)_d_r(bef,h)_d_r(cef,i)_d_r(def,j)
_mem4:_is40(bc,A)_and(dA,B)_d_r(aBe,C)_is41(bc,D)_and(dD,E)_d_r(aEe,F)_is42(bc,G)_and(dG,H)_d_r(aHe,I)_is43(bc,J)_and(dJ,K)_d_r(aKe,L)_mux4(CFILbc,f)
_mem16:_is40(bc,A)_and(fA,B)_mem4(adeBg,C)_is41(bc,D)_and(fD,E)_mem4(adeEg,F)_is42(bc,G)_and(fG,H)_mem4(adeHg,I)_is43(bc,J)_and(fJ,K)_mem4(adeKg,L)_mux4(CFILbc,h)
_mem32:_is40(0b,A)_and(gA,B)_mem16(acdefBh,C)_is41(0b,D)_and(gD,E)_mem16(acdefEh,F)_mux2(CFb,i)
_mem4b32:_mem32(aefghijk,l)_mem32(befghijk,m)_mem32(cefghijk,n)_mem32(defghijk,o)
_alu_m:_andnot(ef,A)_and(ef,B)_mem4b(abcdAh,CDEF)_mem4b(abcdBh,GHIJ)_not(g,K)_alu(CDEFGHIJcdbM,NOPQL)_d_r(LKh,M)_mem4b(NOPQKh,ijkl)
_full_comp:_andnot(ba,I)_and(Ic,J)_andnot(Ic,K)_or(ab,L)_or(Lc,M)_not(M,N)_mem4b32(ijkldefghJN,ABCD)_mux4b2(efghABCDb,EFGH)_alu_m(EFGHacKN,ijkl)
_main:_full_comp(abcdefgh,ijkl)
